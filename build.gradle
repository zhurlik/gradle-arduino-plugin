import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'idea'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'groovy'
    id 'jacoco'
    // for local testing
    id 'maven'
}

group = 'com.github.zhurlik'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'commons-io', name: 'commons-io', version: '2.+'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.+'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.+'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://github.com/zhurlik'
    vcsUrl = 'https://github.com/zhurlik/gradle-arduino-plugin'
    description = 'A gradle plugin for using Arduino IDE in the gradle projects'
    tags = ['gradle', 'gradle-plugin', 'arduino', 'arduino-ide']

    plugins {
        arduinoPlugin {
            id = 'com.github.zhurlik.arduino'
            displayName = 'Gradle Arduino IDE plugin'
        }
    }
}

// for local testing
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('tmp-repo'))
        }
    }
}